TitlePicture:
@includebin _title_zx0.bin
	.even

@include acpu_tiles.mac


; draws number tile, R0 - #, R5 - vaddr
DrawTile:
	mul	#64., R0
	add	#TilesCpuData, R1		; R1 - addr of 16x16 tile
	mov	#16., R3
	mov	#C_SCRWID-3, R2
10$:	movb	(R1)+, (R5)+
	movb	(R1)+, (R5)+
	movb	(R1)+, (R5)+
	movb	(R1)+, (R5)
	add	R2, R5
	sob	R3, 10$
	return


; menu
GotIt:	call	ClScr
	; unpack picture
	mov	#TitlePicture, R0
	mov	#VSCR, R1
	call	Unpack
;	; draw menu
;	mov	#C_MENUY*C_SCRWID+18.+VSCR, R5
;	mov	#MsgMenu, R0
;	call	PrintText
	; draw arena number
	call	PrintArenaNum
	; clear keys
	call	ClearKeys
01$:	; keypress processing
	call	WaitKeypress
	bis	#SND_MENU, SndBm	; menu tick sound
	;
	tst	Key3			; 3 - info
	beq	10$
	clr	Key3
	clr	Key1			; (!) damn that, 1 and 3 in Neon are sharing key bits
	br	OutInfo
10$:	tst	Key1			; 1 - start
	beq	20$
	clr	Key1
	clr	Key3
	call	ClScr
	call	Go
	br	GotIt
20$:	tst	Key2			; 2 - inc arena
	beq	30$
	clr	Key2
	inc	Amap
	cmp	Amap, #5
	blo	22$
	clr	Amap
22$:	call	PrintArenaNum
	br	01$
30$:	tst	Key0			; 0 - exit
	beq	01$
	return


; info out
OutInfo:
	call	ClScr
	mov	#60.*C_SCRWID+06.+VSCR, R5
	mov	#MsgInfo, R0
	call	PrintText
	call	WaitKeypress
	call	ClScr
	mov	#70.*C_SCRWID+12.+VSCR, R5
	mov	#MsgCtrl, R0
	call	PrintText
	call	WaitKeypress
	call	ClScr
	mov	#70.*C_SCRWID+02.+VSCR, R5
	mov	#MsgAuth, R0
	call	PrintText
	call	WaitKeypress
	jmp	GotIt


PrintArenaNum:
	movb	Amap, R0
;	add	#'1, R0
;	movb	R0, MsgNumber+1
;	mov	#C_MENUY+14.*C_SCRWID+36.+VSCR, R5
;	mov	#MsgNumber, R0
;	call	PrintStr
	mov	#C_MENUY+16.*C_SCRWID+41.+VSCR, R5
	call	DrawTile
	return


; ZX0 v2.2 unpacker
; original ZX0 encoder/decoder (c) Einar Saukas & Urusergi
; PDP11 version by reddie, 02-may-2024 (upd)
; R0 = source packed zx0, R1 = dest addr
Unpack:	clr	R2
	movb	#200, R3
	sxt	R5
10$:	call	50$
	movb	(R0)+, (R1)+
	sob	R2, .-2
	aslb	R3
	bcs	30$
	call	50$
20$:	mov	R1, R4
	add	R5, R4
	movb	(R4)+, (R1)+
	sob	R2, .-2
	aslb	R3
	bcc	10$
30$:	mov	#-2, R2
	call	60$
	incb	R2
	bne	.+4
40$:	return
	swab	R2
	mov	R2, R5
	clrb	R5
	bisb	(R0)+, R5
	asr	R5
	mov	#1, R2
	bcs	.+6
	call	80$
	inc	R2
	br	20$
50$:	incb	R2
60$:	aslb	R3
	bne	70$
	movb	(R0)+, R3
	rolb	R3
70$:	bcs	40$
80$:	aslb	R3
	rol	R2
	br	60$ 


;MsgNumber:
;	.asciz	<2>"1"

;MsgMenu:
;	.asciz	<2>"1"<1>": "<3>"START GAME"
;	.asciz	<2>"2"<1>": "<3>"ARENA "
;	.asciz	<2>"3"<1>": "<3>"INFORMATION"
;	.asciz	<1>"0"<1>": "<1>"EXIT TO DOS"
;	.byte	-1

MsgInfo:
	.asciz	<2>"LEGEND"
	.byte	0
	.asciz	<3>"  MONSTERS FROM THE PAST"
	.asciz	<3>"  FOUND MAGIC PORTAL ON"
	.asciz	<3>"  BK 0010 HOME COMPUTERS?"
	.asciz	<3>"  YOU ARE OUR LAST HOPE?"
	.asciz	<3>"  FIGHT WITH THIS EVIL"
	.asciz	<3>"  AND MAY THE FORCE BE"
	.asciz	<3>"  WITH YOU?"
	.asciz	<3>"  YOUR TASK IS TO SCORE"
	.asciz	<3>"  25 POINTS TO WIN?"
	.byte	-1

MsgCtrl:
	.asciz	<2>"CONTROLS"
	.byte	0
	.asciz	<3>"  ARROWS ; MOVE"
	.asciz	<3>"   SPACE ; FIRE"
	.asciz	<3>"   SHIFT ; STRAFE"
	.asciz	<3>"     TAB ; MAP"
	.asciz	<3>"   ENTER ; PAUSE"
	.asciz	<3>"     AP2 ; EXIT"
	.byte	-1

MsgAuth:
	.asciz	<2>"AUTHORS"
	.byte	0
	.asciz	<2>"ENGINE=GAME:"
	.asciz	<3>"   APRAKSIN DMITRY \ADW2RU]"
	.asciz	<2>"TITLE PICTURE:"
	.asciz	<3>"   LASHIN URY \INOART STUDIO]"
	.asciz	<2>"SUPPORT BK COMMUNITY:"
	.asciz	<3>"   T<ME>BK0010[11M"
	.byte	0
	.asciz	<1>"2024 YEAR"
	.byte	-1

	.even


; NRV2d	(UCL) FOR BK/PDP
; INPUT:
; R0 - SOURCE
; R1 - DESTINATION
UNPK:	MOV #-1,R4
	XOR R2,R2
	BR DCL

NXDC:	MOVB (R0)+,(R1)+
DCL:	CALL RB
	BCS NXDC

	MOV #1,R3
OFN:	ADD R3,R3
	CALL RB
	ADC R3
	CALL RB
	BCS OFE
	DEC R3
	ADD R3,R3
	CALL RB
	ADC R3
	BR OFN

RB:	ADD R2,R2
	BEQ RBZ
	RET
RBZ:	MOVB (R0)+,R2
	SWAB R2
	BIC #177,R2
	BIS #200,R2
	ADD R2,R2
	RET

OFE:	SUB #3,R3
	BCC OF2

	MOV R4,R3
	XOR R5,R5
	CALL RB
	ADC R5
	BR OF3

OF2:	SWAB R3
	MOVB (R0)+,R5
	BIC #177400,R5
	BIS R5,R3
	COM R3
	BEQ BRK
	XOR R5,R5
	SEC
	ROR R3
	ADC R5
	MOV R3,R4

OF3:	ADD R5,R5
	CALL RB
	ADC R5
	BNE LF1

	INC R5
NXLF:	ADD R5,R5
	CALL RB
	ADC R5
	CALL RB
	BCC NXLF
	ADD #2,R5

LF1:	CMP R3,#175400
	ADC R5
	INC R5

	ADD R1,R3
NXMV:	MOVB (R3)+,(R1)+
	SOB R5,NXMV
	BR DCL

BRK:	RET 

